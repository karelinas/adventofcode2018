# Annotated version of the program for AoC 2018 day 19
#
# Notation for annotations:
#   X   = direct value X
#   [X] = register X
#   jmp = absolute jump
#   jmr = relative jump, e.g. "jmr 3" jumps to [ip] + 3

            #ip 3

            # Jump to L1 where the target value is initialized in [4]
start:      addi 3 16 3     # jmp L1

            # Program starts here after initialization
L4:         seti 1 2 1      # [1] = 1
L3:         seti 1 1 2      # [2] = 1

            # [5] = [1] * [2]
            # if [5] == [4] then [0] += [1]
            # [2]++
L2:         mulr 1 2 5      # [5] = [1] * [2]
            eqrr 5 4 5      # [5] = [5] == [4]
            addr 5 3 3      # jmr [5] + 1
            addi 3 1 3      # jmp L5
            addr 1 0 0      # [0] += [1]
L5:         addi 2 1 2      # [2]++

            # if [2] <= [4] then jump L2
            # else [1]++
            gtrr 2 4 5      # [5] = [2] > [4]
            addr 3 5 3      # jmr [5] + 1
            seti 2 3 3      # jmp L2
            addi 1 1 1      # [1]++

            # if [1] <= [4] then jump to L3
            # else EXIT
            gtrr 1 4 5      # [5] = [1] > [4]
            addr 5 3 3      # jmr [5] + 1
            seti 1 6 3      # jmp L3
            mulr 3 3 3      # EXIT

            # L1 and onward is run only once.
            # Set [4] to 10551361 and jump to L4 (assuming [0] == 1)
L1:         addi 4 2 4      # [4] += 2
            mulr 4 4 4      # [4] *= [4]
            mulr 3 4 4      # [4] *= 19
            muli 4 11 4     # [4] *= 11
            addi 5 5 5      # [5] += 5
            mulr 5 3 5      # [5] *= 22
            addi 5 15 5     # [5] += 15
            addr 4 5 4      # [4] += [5]
            addr 3 0 3      # jmr [0] + 1
            seti 0 6 3      # jmp L4
            setr 3 5 5      # [5] = 27
            mulr 5 3 5      # [5] *= 28
            addr 3 5 5      # [5] += 29
            mulr 3 5 5      # [5] *= 30
            muli 5 14 5     # [5] *= 14
            mulr 5 3 5      # [5] *= 32
            addr 4 5 4      # [4] += 10550400
            seti 0 5 0      # [0] = 0      # [4] == 10551361
            seti 0 1 3      # jmp L4
